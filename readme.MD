# Compiller

## Запуск

- **Скачать проект**
```
git clone https://github.com/pudge063/compiller
```

- **Создать файл с кодом на языке грамматики (пример test.txt)**
```
begin

(* program body *)

var dim x, y # (* integer variable *);
dim a @; (* float variable *)
dim b &; (* logical variable *)

x assign 2;
y assign 2 * x;
displ y;

a assign 101.10E+3;
a assign 2.12e-2;

b assign true;

dim c @;
c assign 3e+4;

end
```

- **Запуск компилятора**
```
python main.py -f "test.txt"
```

- **Отладка**
```
python main.py -f "test.txt" debug
```


---

# Грамматика пользовательского языка программирования

---

### Операторы

- **Операции группы отношений**:
  ```plaintext
  <операции_группы_отношения> ::= != | == | < | <= | > | >=
  ```
- **Операции группы сложения**:
  ```plaintext
  <операции_группы_сложения> ::= + | - | ||
  ```
- **Операции группы умножения**:
  ```plaintext
  <операции_группы_умножения> ::= * | / | &&
  ```
- **Унарная операция**:
  ```plaintext
  <унарная_операция> ::= !
  ```

---

### Выражения

- **Выражение**:
  ```plaintext
  <выражение> ::= <операнд> {<операции_группы_отношения> <операнд>}
  ```
- **Операнд**:
  ```plaintext
  <операнд> ::= <слагаемое> {<операции_группы_сложения> <слагаемое>}
  ```
- **Слагаемое**:
  ```plaintext
  <слагаемое> ::= <множитель> {<операции_группы_умножения> <множитель>}
  ```
- **Множитель**:
  ```plaintext
  <множитель> ::= <идентификатор> | <число> | <логическая_константа> | <унарная_операция> <множитель> | "(" <выражение> ")"
  ```

---

### Типы данных

- **Число**:
  ```plaintext
  <число> ::= <целое> | <действительное>
  ```
- **Логическая константа**:
  ```plaintext
  <логическая_константа> ::= true | false
  ```

---

### Идентификаторы

- **Идентификатор**:
  ```plaintext
  <идентификатор> ::= <буква> {<буква> | <цифра>}
  ```
- **Буква**:
  ```plaintext
  <буква> ::= A | B | C | ... | Z | a | b | c | ... | z
  ```
- **Цифра**:
  ```plaintext
  <цифра> ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
  ```

---

### Числа

- **Целое число**:
  ```plaintext
  <целое> ::= <двоичное> | <восьмеричное> | <десятичное> | <шестнадцатеричное>
  ```
  - **Двоичное**:
    ```plaintext
    <двоичное> ::= {0 | 1} (B | b)
    ```
  - **Восьмеричное**:
    ```plaintext
    <восьмеричное> ::= {0 | 1 | 2 | 3 | 4 | 5 | 6 | 7} (O | o)
    ```
  - **Десятичное**:
    ```plaintext
    <десятичное> ::= {<цифра>} [D | d]
    ```
  - **Шестнадцатеричное**:
    ```plaintext
    <шестнадцатеричное> ::= <цифра> {<цифра> | A | B | C | D | E | F | a | b | c | d | e | f} (H | h)
    ```

- **Действительное число**:
  ```plaintext
  <действительное> ::= <числовая_строка> <порядок> | [<числовая_строка>] . <числовая_строка> [<порядок>]
  ```
  - **Числовая строка**:
    ```plaintext
    <числовая_строка> ::= {<цифра>}
    ```
  - **Порядок**:
    ```plaintext
    <порядок> ::= (E | e) [+ | -] <числовая_строка>
    ```

---

### Структура программы

- **Программа**:
  ```plaintext
  <программа> ::= begin var <описание> {; <оператор>} end
  ```
- **Описание**:
  ```plaintext
  <описание> ::= dim <идентификатор> {, <идентификатор>} <тип>
  ```
- **Тип** (в порядке: целый, действительный, логический):
  ```plaintext
  <тип> ::= # | @ | &
  ```

---

### Операторы

- **Составной оператор**:
  ```plaintext
  <составной> ::= "[" <оператор> { ( : | перевод строки) <оператор>} "]"
  ```
- **Присваивание**:
  ```plaintext
  <присваивания> ::= <идентификатор> assign <выражение>
  ```
- **Условный оператор**:
  ```plaintext
  <условный> ::= if <выражение> then <оператор> [else <оператор>] end
  ```
- **Фиксированный цикл**:
  ```plaintext
  <фиксированного_цикла> ::= for <присваивания> val <выражение> do <оператор>
  ```
- **Условный цикл**:
  ```plaintext
  <условного_цикла> ::= while <выражение> do <оператор> next
  ```
- **Ввод данных**:
  ```plaintext
  <ввода> ::= enter <идентификатор> {пробел <идентификатор>}
  ```
- **Вывод данных**:
  ```plaintext
  <вывода> ::= displ <выражение> {, <выражение>}
  ```

---

### Комментарии

- **Многострочный комментарий**:
  ```plaintext
  (* ... *)
  ```

---

